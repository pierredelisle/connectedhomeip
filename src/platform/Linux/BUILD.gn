# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/linux/pkg_config.gni")
import("//build_overrides/chip.gni")

import("${chip_root}/src/platform/device.gni")

assert(chip_device_platform == "linux")

if (chip_enable_openthread) {
  import("//build_overrides/openthread.gni")
  import("//build_overrides/ot_br_posix.gni")
}

config("linux_config") {
  defines = [
    "CHIP_DEVICE_LAYER_TARGET_LINUX=1",
    "CHIP_DEVICE_LAYER_TARGET=Linux",
  ]

  if (chip_enable_wifi) {
    defines += [ "CHIP_DEVICE_CONFIG_ENABLE_WPA=1" ]
  }
}

static_library("Linux") {
  output_name = "libDeviceLayerLinux"

  sources = [
    "BLEManagerImpl.cpp",
    "BLEManagerImpl.h",
    "BlePlatformConfig.h",
    "CHIPDevicePlatformConfig.h",
    "CHIPDevicePlatformEvent.h",
    "CHIPLinuxStorage.cpp",
    "CHIPLinuxStorage.h",
    "CHIPLinuxStorageIni.cpp",
    "CHIPLinuxStorageIni.h",
    "CHIPPlatformConfig.h",
    "ConfigurationManagerImpl.cpp",
    "ConfigurationManagerImpl.h",
    "ConnectivityManagerImpl.cpp",
    "ConnectivityManagerImpl.h",
    "InetPlatformConfig.h",
    "Logging.cpp",
    "PlatformManagerImpl.cpp",
    "PlatformManagerImpl.h",
    "PosixConfig.cpp",
    "PosixConfig.h",
    "SystemPlatformConfig.h",
    "SystemTimeSupport.cpp",
  ]

  public_deps = [
    "${chip_root}/src/platform:platform_base",
    "${chip_root}/third_party/inipp",
  ]

  if (chip_enable_openthread) {
    sources += [
      "ThreadStackManagerImpl.cpp",
      "ThreadStackManagerImpl.h",
    ]
    public_deps += [ "${ot_br_posix_root}:ot_br_client" ]
  }

  if (chip_enable_wifi) {
    public_deps += [ "dbus" ]
  }

  public_configs = [ ":linux_config" ]
}
