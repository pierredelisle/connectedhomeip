# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/nrf5_sdk.gni")

import("${chip_root}/src/platform/device.gni")

assert(chip_device_platform == "nrf5")

if (chip_enable_openthread) {
  import("//build_overrides/openthread.gni")
}

config("nrf5_config") {
  defines = [
    "CHIP_DEVICE_LAYER_TARGET_NRF5=1",
    "CHIP_DEVICE_LAYER_TARGET=nRF5",
  ]
}

static_library("nRF5") {
  output_name = "libDeviceLayerNRF5"

  sources = [
    "../FreeRTOS/SystemTimeSupport.cpp",
    "BLEManagerImpl.cpp",
    "BLEManagerImpl.h",
    "BlePlatformConfig.h",
    "CHIPDevicePlatformConfig.h",
    "CHIPDevicePlatformEvent.h",
    "CHIPPlatformConfig.h",
    "ConfigurationManagerImpl.cpp",
    "ConfigurationManagerImpl.h",
    "ConnectivityManagerImpl.cpp",
    "ConnectivityManagerImpl.h",
    "InetPlatformConfig.h",
    "Logging.cpp",
    "PlatformManagerImpl.cpp",
    "PlatformManagerImpl.h",
    "SystemPlatformConfig.h",
    "nRF5Config.cpp",
    "nRF5Config.h",
    "nRF5Utils.cpp",
    "nRF5Utils.h",
  ]

  public_deps = [
    "${chip_root}/src/platform:platform_base",
    "${nrf5_sdk_build_root}:nrf5_sdk",
  ]

  if (chip_enable_openthread) {
    public_deps += [
      "${openthread_root}:libopenthread-cli-ftd",
      "${openthread_root}:libopenthread-ftd",
    ]

    sources += [
      "../OpenThread/OpenThreadUtils.cpp",
      "ThreadStackManagerImpl.cpp",
      "ThreadStackManagerImpl.h",
    ]
  }

  public_configs = [ ":nrf5_config" ]
}
